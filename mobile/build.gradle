buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.4'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 5
        versionPatch 5

    }
    codeOptions {
        versionCodeType org.moallemi.gradle.internal.VersionCodeType.AUTO_INCREMENT_ONE_STEP
        dependsOnTasks 'release'
    }
    outputOptions {
    }
}

def appVersionName = advancedVersioning.versionName
def appVersionCode = advancedVersioning.versionCode

def renameAPK(variant, defaultConfig, buildType) {
    variant.outputs.each { output ->
        def formattedDate = new Date().format('yyyyMMdd')

        def file = output.packageApplication.outputFile
        def fileName = applicationName + "-" + buildType + "-" + defaultConfig.versionName + "." + defaultConfig.versionCode + "-" + formattedDate + "-" + 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim() + ".apk"
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

android {
    signingConfigs {
        config {
            keyAlias "${alias}"
            keyPassword "${password}"
            storeFile file('/Users/cjones/Google Drive/Caleb/Development/BlogSite.keystore')
            storePassword "${password}"
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "me.calebjones.spacelaunchnow"
        manifestPlaceholders = [appNameLabel                   : "@string/application_name",
                                manifestApplicationId          : "${applicationId}",
                                onesignal_app_id               : "${app_id}",
                                onesignal_google_project_number: "${google_project_number}"]
        minSdkVersion 16
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName
        signingConfig signingConfigs.config
        resConfigs "en"
        multiDexEnabled true

    }
    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            manifestPlaceholders = [appNameLabel         : "SLN - Debug",
                                    manifestApplicationId: "me.calebjones.spacelaunchnow.ui.debug"]
            debuggable true
            renderscriptOptimLevel 3
            versionNameSuffix "debug"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'

        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                if (variant.buildType.name == "release") {
                    renameAPK(variant, defaultConfig, "release")
                }
            }
        }

        validation.initWith(debug)
        validation {
            debuggable false
            renderscriptOptimLevel 3
            applicationIdSuffix ".test"
            versionNameSuffix "test"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            testProguardFile('proguard-rules-test.pro')
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        debugTesting {
            minSdkVersion 21
            multiDexEnabled false
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 16
            multiDexEnabled false
        }
    }
}

dependencies {
    wearApp project(':wear')
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile('com.mikepenz:aboutlibraries:5.9.3@aar') {
        transitive = true
    }
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
    compile 'com.afollestad.material-dialogs:commons:0.9.3.0'
    compile "com.android.support:customtabs:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:palette-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-v7:$rootProject.supportLibraryVersion"
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile "com.google.android.gms:play-services-location:$rootProject.playServicesVersion"
    compile "com.google.android.gms:play-services-wearable:$rootProject.playServicesVersion"
    compile "com.google.android.gms:play-services-gcm:$rootProject.playServicesVersion"
    compile 'com.onesignal:OneSignal:3.4.3@aar'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'za.co.riggaroo:materialhelptutorial:1.1.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.borax12.materialdaterangepicker:library:1.9'
    compile 'net.mediavrog:integrated-rating-request:1.1.2'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.11'
    compile 'com.github.javiersantos:MaterialStyledDialogs:2.1'
    compile 'com.karumi:dexter:3.0.2'
    compile 'com.anjlab.android.iab.v3:library:1.0.38'
    compile 'hanks.xyz:smallbang-library:0.1.2'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.github.renaudcerrato:static-maps-api:1.0.2'
    compile 'com.github.pwittchen:weathericonview:1.1.0'
    compile 'android.zetterstrom.com.forecast:forecast:1.2.0'
    compile 'it.macisamuele:calendarprovider-lib:0.0.1'
    compile 'com.github.michael-rapp:android-preference-activity:4.2.10'
    compile 'com.evernote:android-job:1.1.6'
    compile 'blue.aodev:material-values:1.1.1'
    compile 'com.mikepenz:iconics-core:2.8.2@aar'
    compile 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fontawesome-typeface:4.7.0.0@aar'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0'
    compile 'com.pavelsikun:material-seekbar-preference:2.3.0'
    compile 'com.android.support:multidex:1.0.1'
    compile project(path: ':data')
}
