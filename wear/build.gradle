import org.moallemi.gradle.internal.VersionCodeType


buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
    }
}


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'com.getkeepsafe.dexcount'

advancedVersioning {
    nameOptions {
        versionMajor app.versionMajor
        versionMinor app.versionMinor
        versionPatch app.versionPatch
        versionBuild versionCode
    }
    codeOptions {
        versionCodeType VersionCodeType.AUTO_INCREMENT_ONE_STEP
        dependsOnTasks 'release', 'publishReleaseApk'

    }
    outputOptions {
        renameOutput true
        nameFormat '${projectName}-${appName}-${buildType}-${versionName}'
    }
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    google()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

if (keystorePropertiesFile.canRead()) {
// Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    throw new GradleException("Could not read keystore.properties!")
}

android {

    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    def appVersionCode = 2000000 + app.versionMajor * 10000 + app.versionMinor * 1000 + app.versionPatch * 100 + advancedVersioning.versionCode

    defaultConfig {
        flavorDimensions "release"
        manifestPlaceholders = [applicationLabel: "@string/app_name"]
        applicationId "me.calebjones.spacelaunchnow"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode appVersionCode
        versionName advancedVersioning.versionName
    }
    buildTypes {
        release {
//            splits {
//                abi {
//                    enable true
//                    reset()
//                    include 'x86_64', 'x86', 'armeabi-v7a', 'arm64-v8a', 'mips'
//                    universalApk true
//                }
//            }
            signingConfig signingConfigs.releaseConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug-b${appVersionCode}"
            manifestPlaceholders = [applicationLabel: "SLN - Debug"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

//play {
//    serviceAccountCredentials = rootProject.file("publisher-key.json")
//    track = "internal"
//    releaseStatus = "draft"
//}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':data')
    implementation 'com.jakewharton:butterknife:9.0.0-SNAPSHOT'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-SNAPSHOT'
    compileOnly 'com.google.android.wearable:wearable:2.4.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'com.pixplicity.easyprefs:library:1.9.0'
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'androidx.wear:wear:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.0.0'
    implementation 'com.google.android.support:wearable:2.4.0'
    implementation "com.google.android.gms:play-services-wearable:16.0.1"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'blue.aodev:material-values:1.1.1'
}
